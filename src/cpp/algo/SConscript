# -*-python-*-
import os
from glob import glob

pj= os.path.join

Import( "env" )

# 1. Select and install the headers

include_dir = str(env.Dir( "." ).srcnode())

h_files= pj(include_dir,'*.h')
includes= glob(h_files)

# 2. Call other SConscript files in sub directories and build the library

lib_env= env.Copy()
lib_env.ALEAIncludes( "plantgl/algo", includes )

# dirs = [d for d in glob('*') if os.path.isdir(d)]
sub_dirs= glob(include_dir+'/*')
dirs = [ pj ( os.path.basename(d),'SConscript' )
         for d in sub_dirs if os.path.isdir(d) ]

sources= []
for d in dirs:
    src= lib_env.SConscript( d, exports = { "env" : lib_env } )
    if src: sources+= src

# Add defines to export symbols on Windows
DEFINES= list(lib_env['CPPDEFINES'])
DEFINES.append('ALGO_MAKEDLL')

# Link with the following libraries
LIBRARIES = list( lib_env['LIBS'] )
LIBRARIES.extend( ['pgltool','pglmath','pglsg'] )

lib_env.ALEALibrary( "pglalgo",
                     sources, 
                     CPPDEFINES = DEFINES, 
                     LIBS = LIBRARIES )


